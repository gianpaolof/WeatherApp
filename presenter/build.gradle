plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
}

def adMob_key_value = System.getenv("ADMOB_KEY")

android {
    namespace 'com.reza.weatherapp'
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.reza.weatherapp"
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [adMob_key: "$System.env.ADMOB_KEY"]
    } 

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ext.config = getProps('../config/release.properties')
            ext.config.each { p ->
                if (p.key == "APP_NAME") {
                    resValue "string", "app_name", p.value
                } else if (p.key == "APPLICATION_ID_SUFFIX") {
                    applicationIdSuffix p.value
                } else
                    buildConfigField 'String', p.key, p.value
            }
        }
        staging {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ext.config = getProps('../config/staging.properties')
            ext.config.each { p ->
                if (p.key == "APP_NAME") {
                    resValue "string", "app_name", p.value
                } else if (p.key == "APPLICATION_ID_SUFFIX") {
                    applicationIdSuffix p.value
                } else {
                    buildConfigField 'String', p.key, p.value
                }
            }
        }
        benchmark {
            signingConfig signingConfigs.debug
            matchingFallbacks = ['release']
             
            ext.config = getProps('../config/release.properties')
            ext.config.each { p ->
                if (p.key == "APP_NAME") {
                    resValue "string", "app_name", p.value
                } else if (p.key == "APPLICATION_ID_SUFFIX") {
                    applicationIdSuffix p.value
                } else {
                    buildConfigField 'String', p.key, p.value
                }
            }
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.config = getProps('../config/development.properties')
            ext.config.each { p ->
                if (p.key == "APP_NAME") {
                    resValue "string", "app_name", p.value
                } else if (p.key == "APPLICATION_ID_SUFFIX") {
                    applicationIdSuffix p.value
                } else
                    buildConfigField 'String', p.key, p.value
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation rootProject.ext.constraintlayout

    // Dagger2
    implementation rootProject.ext.dagger
    kapt rootProject.ext.daggerCompiler

    // RxJava2
    implementation rootProject.rxJava
    implementation rootProject.rxAndroid

    // Gson
    implementation rootProject.gson

    // Glide
    implementation rootProject.glide
    annotationProcessor rootProject.glideAnnotationProcessor

    // Networking
    implementation rootProject.retrofit
    implementation rootProject.retrofitGsonConverter
    implementation rootProject.retrofitRxJava2Adapter
    implementation rootProject.okHttp3
    implementation rootProject.okHttp3LoggingInterceptor

    // Logging
    implementation rootProject.timber

    // SharedPref
    implementation rootProject.rxSharedPref

    // Lottie
    implementation rootProject.lottie

    // Firebase
    implementation platform(rootProject.ext.firebaseBom)
    implementation rootProject.ext.firebaseAnalytics
    implementation rootProject.ext.addMob

    // To fix Duplicate class kotlin.random.jdk8 found in modules jetified-kotlin-stdlib-1.8.10, with start-up benchmark
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))

    // Tests
    testImplementation rootProject.junit
    testImplementation rootProject.mockitoCore
    testImplementation rootProject.mockitoKotlin
    testImplementation rootProject.truth

    // Android Test
    androidTestImplementation rootProject.extJunit
    androidTestImplementation rootProject.espresso

    // Projects
    implementation project(':data')
    implementation project(':domain')
}

File signPropsFile = file('../signing/release.props')
if (signPropsFile.exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(signPropsFile))
    android {
        signingConfigs {
            release {
                storeFile file('../signing/release.keystore')
                storePassword props['storePass']
                keyAlias props['keyAlias']
                keyPassword props['keyPass']
            }
            staging {
                storeFile file('../signing/release.keystore')
                storePassword props['storePass']
                keyAlias props['keyAlias']
                keyPassword props['keyPass']
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
            staging {
                signingConfig signingConfigs.release
            }
        }
    }
} else {
    android {
        buildTypes {
            release {
                signingConfig signingConfigs.debug
            }
        }
    }
}

def getProps(path) {
    Properties props = new Properties()
    props.load(new FileInputStream(file(path)))
    return props
}